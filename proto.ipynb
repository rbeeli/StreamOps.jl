{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using DataStructures\n",
    "using Dates\n",
    "\n",
    "\"\"\"\n",
    "A node in a stream computation graph that represents a computation step.\n",
    "\"\"\"\n",
    "mutable struct StreamNode{TGraph}\n",
    "    graph::TGraph\n",
    "    index::Int\n",
    "    func::Function\n",
    "    inputs::Vector{StreamNode}\n",
    "    label::String\n",
    "    function StreamNode(graph::TGraph, index, func, label) where {TGraph}\n",
    "        new{TGraph}(graph, index, func, StreamNode[], label)\n",
    "    end\n",
    "end\n",
    "\n",
    "@inline is_source(node::StreamNode) = isempty(node.inputs)\n",
    "\n",
    "struct Event{TTime}\n",
    "    timestamp::TTime\n",
    "    trigger_index::Int\n",
    "    Event(timestamp::TTime, trigger_index::Int) where {TTime} = new{TTime}(timestamp, trigger_index)\n",
    "end\n",
    "\n",
    "@inline Base.isless(a::Event, b::Event) = a.timestamp < b.timestamp\n",
    "\n",
    "\"\"\"\n",
    "A directed acyclic graph (DAG) that represents a stream computation graph.\n",
    "\"\"\"\n",
    "mutable struct StreamGraph\n",
    "    nodes::Vector{StreamNode{StreamGraph}}\n",
    "    deps::Vector{Vector{Int}}\n",
    "    reverse_deps::Vector{Vector{Int}}\n",
    "    topo_order::Vector{Int}\n",
    "    function StreamGraph()\n",
    "        new(\n",
    "            StreamNode{StreamGraph}[],\n",
    "            Vector{Int}[],\n",
    "            Vector{Int}[],\n",
    "            Int[]\n",
    "        )\n",
    "    end\n",
    "end\n",
    "\n",
    "function topological_sort!(graph::StreamGraph)\n",
    "    in_degree = [length(deps) for deps in graph.deps]\n",
    "    queue = Int[]\n",
    "\n",
    "    for (index, degree) in enumerate(in_degree)\n",
    "        if degree == 0\n",
    "            push!(queue, index)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    while !isempty(queue)\n",
    "        node_index = pop!(queue)\n",
    "        push!(graph.topo_order, node_index)\n",
    "\n",
    "        for dependent_index in graph.reverse_deps[node_index]\n",
    "            in_degree[dependent_index] -= 1\n",
    "            if in_degree[dependent_index] == 0\n",
    "                push!(queue, dependent_index)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if length(graph.topo_order) != length(graph.nodes)\n",
    "        error(\"Graph has a cycle\")\n",
    "    end\n",
    "\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function node!(graph::StreamGraph, func::Function; label::String=\"\")\n",
    "    if isempty(label)\n",
    "        # label = string(func)\n",
    "        buffer = IOBuffer()\n",
    "        Base.show_unquoted(buffer, func)\n",
    "        label = String(take!(buffer))\n",
    "    end\n",
    "    index = length(graph.nodes) + 1\n",
    "    node = StreamNode(graph, index, func, label)\n",
    "    push!(graph.nodes, node)\n",
    "    push!(graph.deps, Int[])\n",
    "    push!(graph.reverse_deps, Int[])\n",
    "    node\n",
    "end\n",
    "\n",
    "function bind_inputs!(graph::StreamGraph, node::StreamNode, inputs)\n",
    "    node.inputs = collect(inputs)\n",
    "    input_indices = [input.index for input in node.inputs]\n",
    "    graph.deps[node.index] = input_indices\n",
    "    for input_index in input_indices\n",
    "        push!(graph.reverse_deps[input_index], node.index)\n",
    "    end\n",
    "end\n",
    "\n",
    "# syntactic sugar for binding inputs (bind_inputs!)\n",
    "function Base.:|>(inputs, node::StreamNode{G}) where {G}\n",
    "    for (i, input) in enumerate(inputs)\n",
    "        input isa StreamNode || error(\"Input #$i is not a StreamNode\")\n",
    "    end\n",
    "    bind_inputs!(node.graph, node, collect(StreamNode, inputs))\n",
    "    node\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "An executor that runs a stream computation graph.\n",
    "\"\"\"\n",
    "mutable struct Executor{TTime}\n",
    "    graph::StreamGraph\n",
    "    event_queue::BinaryMinHeap{Event{TTime}}\n",
    "    states::Vector{Any}\n",
    "    current_time::TTime\n",
    "    function Executor(graph::StreamGraph, start_time::TTime) where {TTime}\n",
    "        states = Vector{Any}(nothing, length(graph.nodes))\n",
    "        new{TTime}(graph, BinaryMinHeap{Event{TTime}}(), states, start_time)\n",
    "    end\n",
    "end\n",
    "\n",
    "@inline function time(executor::Executor{TTime})::TTime where {TTime}\n",
    "    executor.current_time\n",
    "end\n",
    "\n",
    "function execute_node!(executor::Executor{TTime}, node::StreamNode{StreamGraph}, is_event_trigger::Bool) where {TTime}\n",
    "    if is_source(node)\n",
    "        if is_event_trigger\n",
    "            # get the actual value from the source function\n",
    "            executor.states[node.index] = node.func(time(executor), executor, node)\n",
    "        end\n",
    "    else\n",
    "        if any(isnothing(executor.states[x.index]) for x in node.inputs)\n",
    "            executor.states[node.index] = nothing\n",
    "        else\n",
    "            input_values = (executor.states[x.index] for x in node.inputs)\n",
    "            executor.states[node.index] = node.func(time(executor), input_values...)\n",
    "        end\n",
    "    end\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function process_subgraph!(executor::Executor{TTime}, start_node_index::Int) where {TTime}\n",
    "    graph = executor.graph\n",
    "    nodes = graph.nodes\n",
    "    queue = PriorityQueue{Int,Int}()\n",
    "    enqueue!(queue, start_node_index => 0)\n",
    "    depths = Dict(start_node_index => 0)\n",
    "\n",
    "    while !isempty(queue)\n",
    "        node_index = dequeue!(queue)\n",
    "        is_event_trigger = (node_index == start_node_index)\n",
    "        execute_node!(executor, nodes[node_index], is_event_trigger)\n",
    "\n",
    "        current_depth = depths[node_index]\n",
    "        for dependent_index in graph.reverse_deps[node_index]\n",
    "            new_depth = current_depth + 1\n",
    "            if get(depths, dependent_index, -1) < new_depth\n",
    "                depths[dependent_index] = new_depth\n",
    "                if haskey(queue, dependent_index)\n",
    "                    queue[dependent_index] = -new_depth\n",
    "                else\n",
    "                    enqueue!(queue, dependent_index => -new_depth)\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function process_event!(executor::Executor{TTime}, event::Event{TTime}) where {TTime}\n",
    "    executor.current_time = event.timestamp\n",
    "    process_subgraph!(executor, event.trigger_index)\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function enqueue_event!(executor::Executor{TTime}, timestamp::TTime, trigger_index::Int) where {TTime}\n",
    "    push!(executor.event_queue, Event(timestamp, trigger_index))\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function run_simulation!(executor::Executor)\n",
    "    # initialize source nodes\n",
    "    for node in executor.graph.nodes\n",
    "        if is_source(node)\n",
    "            execute_node!(executor, node, true)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # process events in order of their timestamps\n",
    "    while !isempty(executor.event_queue)\n",
    "        event = pop!(executor.event_queue)\n",
    "        process_event!(executor, event)\n",
    "    end\n",
    "\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function create_list_source(data::D) where {D<:AbstractVector}\n",
    "    current_index = Ref(1)\n",
    "\n",
    "    function source_func(time::TTime, executor::Executor{TTime}, node::StreamNode{StreamGraph}) where {TTime}\n",
    "        if current_index[] <= length(data)\n",
    "            timestamp, output = data[current_index[]]\n",
    "            current_index[] += 1\n",
    "            if current_index[] <= length(data)\n",
    "                next_timestamp, _ = data[current_index[]]\n",
    "                enqueue_event!(executor, next_timestamp, node.index)\n",
    "            end\n",
    "            return output\n",
    "        end\n",
    "        nothing\n",
    "    end\n",
    "\n",
    "    source_func\n",
    "end\n",
    "\n",
    "# Example usage\n",
    "g = StreamGraph()\n",
    "\n",
    "# Create source nodes\n",
    "source1 = node!(g, create_list_source([\n",
    "        (DateTime(2000, 1, 1, 0, 0, 1), 2),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 3), 4),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 5), 6)\n",
    "    ]), label=\"source1\")\n",
    "source2 = node!(g, create_list_source([\n",
    "        (DateTime(2000, 1, 1, 0, 0, 2), 10),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 4), 20),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 6), 30)\n",
    "    ]), label=\"source2\")\n",
    "source3 = node!(g, create_list_source([\n",
    "        (DateTime(2000, 1, 1, 0, 0, 2), 10),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 4), 20),\n",
    "        (DateTime(2000, 1, 1, 0, 0, 6), 30)\n",
    "    ]), label=\"source3\")\n",
    "\n",
    "# Create compute nodes\n",
    "square = node!(g, (time, x) -> x^2; label=\"square\")\n",
    "divide_by_2 = node!(g, (time, x) -> x / 2, label=\"divide_by_2\")\n",
    "negate = node!(g, (time, x) -> -x, label=\"negate\")\n",
    "combine = node!(g, (time, x, y) -> (x, y), label=\"combine\")\n",
    "final_multiply = node!(g, (time, tuple, src2_val, src3_val) -> tuple .* src2_val .+ src3_val, label=\"final_multiply\")\n",
    "output = node!(g, (time, x) -> println(\"Final Output at time $time: $x\"), label=\"output\")\n",
    "\n",
    "# Bind inputs\n",
    "[source1] |> square\n",
    "[square] |> divide_by_2\n",
    "[source2] |> negate\n",
    "(divide_by_2, negate) |> combine\n",
    "(combine, source2, source3) |> final_multiply\n",
    "[final_multiply] |> output\n",
    "\n",
    "# Perform topological sort\n",
    "topological_sort!(g)\n",
    "\n",
    "# Create executor and run simulation\n",
    "executor = Executor(g, DateTime(2000, 1, 1))\n",
    "run_simulation!(executor)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Colors\n",
    "using CairoMakie\n",
    "using GraphMakie\n",
    "using Graphs\n",
    "using LayeredLayouts\n",
    "\n",
    "function visualize_graph(graph::StreamGraph)\n",
    "    nodes = graph.nodes\n",
    "    g = SimpleDiGraph(length(nodes))\n",
    "    nlabels = String[]\n",
    "    node_colors = []\n",
    "    \n",
    "    # Create a mapping from node IDs to graph indices\n",
    "    zero_levels = Vector{Pair{Int, Int}}()\n",
    "    nlabels_align = []\n",
    "    nlabels_color = []\n",
    "\n",
    "    for (i, node) in enumerate(nodes)\n",
    "        node_id = node.index\n",
    "        push!(nlabels, node.label)\n",
    "        if is_source(node)\n",
    "            push!(node_colors, colorant\"#ffdd33\")  # yellow for source nodes\n",
    "            push!(zero_levels, i => 1) # source layers always at level 1\n",
    "            push!(nlabels_align, (:center, :bottom))\n",
    "            push!(nlabels_color, colorant\"#bf489d\")\n",
    "        else\n",
    "            push!(node_colors, colorant\"#b4dee8\")  # light blue for compute nodes\n",
    "            push!(nlabels_align, (:center, :top))\n",
    "            push!(nlabels_color, colorant\"#000000\")\n",
    "        end\n",
    "        \n",
    "        for dep_id in graph.deps[node_id]\n",
    "            add_edge!(g, nodes[dep_id].index, nodes[node_id].index)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    xs, ys, paths = solve_positions(Zarate(), g) #; force_layer=zero_levels)\n",
    "    xs, ys = ys, -xs # rotate coordinates by 90Â°\n",
    "    ys .*= 0.5 # scale the y coordinates\n",
    "\n",
    "    f, ax, p = graphplot(g;\n",
    "        layout=Point.(zip(xs, ys)),\n",
    "        arrow_size=15,\n",
    "        arrow_shift=:end,\n",
    "        arrow_marker='>',\n",
    "        edge_width=0.75,\n",
    "        edge_color=colorant\"#444\",\n",
    "        # node_color=node_colors,\n",
    "        # node_size=48,\n",
    "        nlabels=nlabels,\n",
    "        nlabels_fontsize=14,\n",
    "        nlabels_align=nlabels_align,\n",
    "        nlabels_color=nlabels_color,\n",
    "        # nlabels_distance=12,\n",
    "        node_size=48,\n",
    "        node_color=:white,\n",
    "        nlabels_distance=-10,\n",
    "    )\n",
    "    hidedecorations!(ax)\n",
    "    hidespines!(ax)\n",
    "\n",
    "    # add some padding\n",
    "    x_range, y_range = extrema(xs), extrema(ys)\n",
    "    x_range, y_range = x_range[2] - x_range[1], y_range[2] - y_range[1]\n",
    "    xlims!(ax, minimum(xs) - 0.2x_range, maximum(xs) + 0.2x_range)\n",
    "    ylims!(ax, minimum(ys) - 0.1y_range, maximum(ys) + 0.1y_range)\n",
    "\n",
    "    # adjust width to match aspect ratio\n",
    "    height = size(f.scene)[2]\n",
    "    width = floor(Int, height*(x_range / y_range))\n",
    "    resize!(f, width, height)\n",
    "    \n",
    "    f, ax, p\n",
    "end\n",
    "\n",
    "f, ax, p = visualize_graph(g)\n",
    "\n",
    "# Save the plot to a file\n",
    "# save(\"computation_graph.png\", f)\n",
    "\n",
    "display(f);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
