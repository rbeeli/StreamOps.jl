{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 27,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Final Output at time 2000-01-01T00:00:02: (0.0, -100.0)\n",
      "Final Output at time 2000-01-01T00:00:02: (10.0, -90.0)\n",
      "Final Output at time 2000-01-01T00:00:03: (30.0, -90.0)\n",
      "Final Output at time 2000-01-01T00:00:04: (40.0, -80.0)\n",
      "Final Output at time 2000-01-01T00:00:04: (60.0, -380.0)\n",
      "Final Output at time 2000-01-01T00:00:05: (180.0, -380.0)\n",
      "Final Output at time 2000-01-01T00:00:06: (260.0, -880.0)\n",
      "Final Output at time 2000-01-01T00:00:06: (270.0, -870.0)\n"
     ]
    }
   ],
   "source": [
    "using DataStructures\n",
    "using Dates\n",
    "import Random: randstring\n",
    "\n",
    "\"\"\"\n",
    "A node in a stream computation graph that represents a computation step.\n",
    "\"\"\"\n",
    "mutable struct StreamNode{TGraph}\n",
    "    graph::TGraph\n",
    "    index::Int\n",
    "    func::Function\n",
    "    inputs::Vector{StreamNode}\n",
    "    output_type::Type\n",
    "    init_value::Any\n",
    "    label::String\n",
    "    field_name::Symbol\n",
    "    function StreamNode(graph::TGraph, index, func, output_type, init_value, label) where {TGraph}\n",
    "        field_name = Symbol(\"node$(index)_$(label)\")\n",
    "        new{TGraph}(graph, index, func, StreamNode[], output_type, init_value, label, field_name)\n",
    "    end\n",
    "end\n",
    "\n",
    "@inline is_source(node::StreamNode) = isempty(node.inputs)\n",
    "\n",
    "\"\"\"\n",
    "A directed acyclic graph (DAG) that represents a stream computation graph.\n",
    "\"\"\"\n",
    "mutable struct StreamGraph\n",
    "    nodes::Vector{StreamNode{StreamGraph}}\n",
    "    deps::Vector{Vector{Int}}\n",
    "    reverse_deps::Vector{Vector{Int}}\n",
    "    topo_order::Vector{Int}\n",
    "    function StreamGraph()\n",
    "        new(\n",
    "            StreamNode{StreamGraph}[],\n",
    "            Vector{Int}[],\n",
    "            Vector{Int}[],\n",
    "            Int[]\n",
    "        )\n",
    "    end\n",
    "end\n",
    "\n",
    "function topological_sort!(graph::StreamGraph)\n",
    "    in_degree = [length(deps) for deps in graph.deps]\n",
    "    queue = Int[]\n",
    "\n",
    "    for (index, degree) in enumerate(in_degree)\n",
    "        if degree == 0\n",
    "            push!(queue, index)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    while !isempty(queue)\n",
    "        node_index = pop!(queue)\n",
    "        push!(graph.topo_order, node_index)\n",
    "\n",
    "        for dependent_index in graph.reverse_deps[node_index]\n",
    "            in_degree[dependent_index] -= 1\n",
    "            if in_degree[dependent_index] == 0\n",
    "                push!(queue, dependent_index)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    if length(graph.topo_order) != length(graph.nodes)\n",
    "        error(\"Graph has a cycle\")\n",
    "    end\n",
    "\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function node!(::Type{TOutput}, graph::StreamGraph, func::Function; init=nothing, label::String=\"\") where {TOutput}\n",
    "    if isempty(label)\n",
    "        # label = string(func)\n",
    "        buffer = IOBuffer()\n",
    "        Base.show_unquoted(buffer, func)\n",
    "        label = String(take!(buffer))\n",
    "    end\n",
    "    index = length(graph.nodes) + 1\n",
    "    node = StreamNode(graph, index, func, TOutput, init, label)\n",
    "    push!(graph.nodes, node)\n",
    "    push!(graph.deps, Int[])\n",
    "    push!(graph.reverse_deps, Int[])\n",
    "    node\n",
    "end\n",
    "\n",
    "function bind_inputs!(graph::StreamGraph, node::StreamNode, inputs)\n",
    "    node.inputs = collect(inputs)\n",
    "    input_indices = [input.index for input in node.inputs]\n",
    "    graph.deps[node.index] = input_indices\n",
    "    for input_index in input_indices\n",
    "        push!(graph.reverse_deps[input_index], node.index)\n",
    "    end\n",
    "end\n",
    "\n",
    "# syntactic sugar for binding inputs (bind_inputs!)\n",
    "function Base.:|>(inputs, node::StreamNode{G}) where {G}\n",
    "    for (i, input) in enumerate(inputs)\n",
    "        input isa StreamNode || error(\"Input #$i is not a StreamNode\")\n",
    "    end\n",
    "    bind_inputs!(node.graph, node, collect(StreamNode, inputs))\n",
    "    node\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "Compile the states struct for the given graph to store intermediate results\n",
    "of computation steps.\n",
    "\"\"\"\n",
    "function compile_states(graph::StreamGraph)\n",
    "    # Generate a unique name for the struct\n",
    "    struct_name = Symbol(\"GraphStates_\" * randstring(8))\n",
    "\n",
    "    field_defs = []\n",
    "    constructor_args = []\n",
    "    for (i, node) in enumerate(graph.nodes)\n",
    "        field_type = :($(Union{node.output_type,typeof(node.init_value)}))\n",
    "        push!(field_defs, Expr(:(::), node.field_name, field_type))\n",
    "        push!(constructor_args, node.init_value)\n",
    "    end\n",
    "\n",
    "    struct_def = Expr(:struct, true, struct_name, Expr(:block, field_defs...))\n",
    "    Core.eval(@__MODULE__, struct_def)\n",
    "\n",
    "    constructor_def = :($struct_name() = $struct_name($(constructor_args...)))\n",
    "    Core.eval(@__MODULE__, constructor_def)\n",
    "\n",
    "    # println(\"Generated struct definition:\")\n",
    "    # println(struct_def)\n",
    "    # println(\"Generated constructor:\")\n",
    "    # println(constructor_def)\n",
    "\n",
    "    getfield(@__MODULE__, struct_name)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "An executor that runs a stream computation graph.\n",
    "\"\"\"\n",
    "mutable struct Executor{TStates,TTime}\n",
    "    graph::StreamGraph\n",
    "    states::TStates\n",
    "    current_time::TTime\n",
    "    function Executor(graph::StreamGraph, states::TStates, start_time::TTime) where {TStates,TTime}\n",
    "        new{TStates,TTime}(graph, states, start_time)\n",
    "    end\n",
    "end\n",
    "\n",
    "@inline function time(executor::Executor{TStates,TTime})::TTime where {TStates,TTime}\n",
    "    executor.current_time\n",
    "end\n",
    "\n",
    "@inline function time!(executor::Executor{TStates,TTime}, new_time::TTime) where {TStates,TTime}\n",
    "    executor.current_time = new_time\n",
    "    nothing\n",
    "end\n",
    "\n",
    "function compile_source!(executor::Executor{TStates,TTime}, source_node::StreamNode{StreamGraph}; debug=false) where {TStates,TTime}\n",
    "    graph = executor.graph\n",
    "    nodes = graph.nodes\n",
    "\n",
    "    # Find the subgraph starting from the source node\n",
    "    subgraph_indices = Int[]\n",
    "    queue = [source_node.index]\n",
    "    visited = falses(length(nodes))\n",
    "\n",
    "    while !isempty(queue)\n",
    "        node_index = popfirst!(queue)\n",
    "        if !visited[node_index]\n",
    "            push!(subgraph_indices, node_index)\n",
    "            visited[node_index] = true\n",
    "            append!(queue, graph.reverse_deps[node_index])\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Sort the subgraph indices according to the topological order\n",
    "    sort!(subgraph_indices, by=i -> graph.topo_order[i])\n",
    "\n",
    "    # Generate code for each node in the subgraph\n",
    "    node_expressions = Expr[]\n",
    "    for node_index in subgraph_indices\n",
    "        node = nodes[node_index]\n",
    "        field_name = node.field_name\n",
    "        res_name = Symbol(\"res_$(field_name)\")\n",
    "        if is_source(node)\n",
    "            push!(node_expressions, :(states.$field_name = input_value))\n",
    "        else\n",
    "            input_exprs = [:(states.$(input.field_name)) for input in node.inputs]\n",
    "            if debug\n",
    "                println(\"Node $(node.label) inputs: \", input_exprs)\n",
    "                result_expr = quote\n",
    "                    try\n",
    "                        result = $(node.func)(executor, $(input_exprs...))\n",
    "                        println(\"Node $($(node.label)) output: \", result)\n",
    "                        result::$(node.output_type)  # Type assertion\n",
    "                    catch e\n",
    "                        error(\"Error in node $($(node.label)): $e\")\n",
    "                    end\n",
    "                end\n",
    "            else\n",
    "                result_expr = :($(node.func)(executor, $(input_exprs...)))\n",
    "            end\n",
    "            update_expr = quote\n",
    "                # for input in $input_exprs\n",
    "                #     # print value of input\n",
    "                #     println(\"Input value: \", input)\n",
    "                # end\n",
    "                # abort if any input nothing\n",
    "                # println(\"Inputs $($(node.label)): \", ($([:(states.$(input.field_name)) for input in node.inputs]...),))\n",
    "                if any(isnothing, ($([:(states.$(input.field_name)) for input in node.inputs]...),))\n",
    "                    return\n",
    "                end\n",
    "                $res_name = $result_expr\n",
    "                if !isnothing($res_name)\n",
    "                    states.$field_name = $res_name\n",
    "                end\n",
    "            end\n",
    "            push!(node_expressions, update_expr)\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # println(\"Generated code for source node $(source_node.label):\")\n",
    "    # for expr in node_expressions\n",
    "    #     println(expr)\n",
    "    # end\n",
    "\n",
    "    # Create the compiled function\n",
    "    compiled_func = @eval begin\n",
    "        (executor::Executor{$TStates,$TTime}, time::$TTime, input_value::$(source_node.output_type)) -> begin\n",
    "            executor.current_time = time\n",
    "            states = executor.states\n",
    "            $(node_expressions...)\n",
    "            nothing\n",
    "        end\n",
    "    end\n",
    "\n",
    "    compiled_func\n",
    "end\n",
    "\n",
    "# Example usage\n",
    "g = StreamGraph()\n",
    "\n",
    "# Create source nodes\n",
    "source1 = node!(Float64, g, (exe, x) -> x; init=0.0, label=\"source1\")\n",
    "source2 = node!(Float64, g, (exe, x) -> x; init=0.0, label=\"source2\")\n",
    "source3 = node!(Float64, g, (exe, x) -> x; init=0.0, label=\"source3\")\n",
    "\n",
    "# Create compute nodes\n",
    "square = node!(Float64, g, (exe, x) -> x^2; label=\"square\")\n",
    "divide_by_2 = node!(Float64, g, (exe, x) -> x / 2; label=\"divide_by_2\")\n",
    "negate = node!(Float64, g, (exe, x) -> -x; label=\"negate\")\n",
    "combine = node!(Tuple{Float64,Float64}, g, (exe, x, y) -> (x, y); label=\"combine\")\n",
    "final_multiply = node!(Tuple{Float64,Float64}, g, (exe, tuple, src2, src3) -> tuple .* src2 .+ src3; label=\"final_multiply\")\n",
    "output = node!(Nothing, g, (exe, x) -> println(\"Final Output at time $(time(exe)): $x\"); label=\"output\")\n",
    "\n",
    "# Create edges between nodes (define the computation graph)\n",
    "[source1] |> square\n",
    "[square] |> divide_by_2\n",
    "[source2] |> negate\n",
    "(divide_by_2, negate) |> combine\n",
    "(combine, source2, source3) |> final_multiply\n",
    "[final_multiply] |> output\n",
    "\n",
    "# Perform topological sort\n",
    "topological_sort!(g)\n",
    "\n",
    "# Create executor\n",
    "states = compile_states(g)();\n",
    "executor = Executor(g, states, DateTime(2000, 1, 1))\n",
    "\n",
    "source1_func = compile_source!(executor, source1; debug=!true)\n",
    "source2_func = compile_source!(executor, source2; debug=!true)\n",
    "source3_func = compile_source!(executor, source3; debug=!true)\n",
    "\n",
    "# call the compiled source functions with test data\n",
    "source1_func(executor, DateTime(2000, 1, 1, 0, 0, 1), 2.0)\n",
    "source2_func(executor, DateTime(2000, 1, 1, 0, 0, 2), 10.0)\n",
    "source3_func(executor, DateTime(2000, 1, 1, 0, 0, 2), 10.0)\n",
    "source1_func(executor, DateTime(2000, 1, 1, 0, 0, 3), 4.0)\n",
    "source3_func(executor, DateTime(2000, 1, 1, 0, 0, 4), 20.0)\n",
    "source2_func(executor, DateTime(2000, 1, 1, 0, 0, 4), 20.0)\n",
    "source1_func(executor, DateTime(2000, 1, 1, 0, 0, 5), 6.0)\n",
    "source2_func(executor, DateTime(2000, 1, 1, 0, 0, 6), 30.0)\n",
    "source3_func(executor, DateTime(2000, 1, 1, 0, 0, 6), 30.0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@code_warntype source1_func(executor, DateTime(2000, 1, 1, 0, 0, 1), 2.0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using BenchmarkTools\n",
    "@benchmark compile_source!(executor, source1) samples=500 evals=2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using BenchmarkTools\n",
    "@benchmark compile_source2!(executor, source1) samples=500 evals=2"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
